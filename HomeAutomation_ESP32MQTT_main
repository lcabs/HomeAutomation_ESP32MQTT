/* Headers */ 
#include <WiFi.h> 								// Header para uso das funcionalidades de wi-fi do ESP32
#include <PubSubClient.h>  						//  Header para uso da biblioteca PubSubClient
#include <SPI.h>
#include <Wire.h>
#include <Adafruit_GFX.h>
#include <Adafruit_SSD1306.h>
 
/* Defines do MQTT */
#define ID_MQTT  "ESP32_lcabs1993"     			// id mqtt (para identificação de sessão)
const char* BROKER_MQTT = "broker.hivemq.com"; 	// URL do broker MQTT que deseja utilizar
int BROKER_PORT = 1883;							// Porta do Broker MQTT 

/* Defines do Display */
#define SCREEN_WIDTH 	128 					// OLED display width, in pixels
#define SCREEN_HEIGHT 	64 						// OLED display height, in pixels

// Declaration for an SSD1306 display connected to I2C (SDA, SCL pins)
#define OLED_RESET     4 						// Reset pin # (or -1 if sharing Arduino reset pin)
Adafruit_SSD1306 display(SCREEN_WIDTH, SCREEN_HEIGHT, &Wire, OLED_RESET);

/*  Variáveis e constantes globais */
const char* SSID = 		"EG8115V5"; 			// SSID: nome da rede WI-FI que deseja se conectar
const char* PASSWORD = 	"alpha147147"; 			// Senha da rede WI-FI que deseja se conectar
  
 /* Variáveis e objetos globais */
WiFiClient espClient;
PubSubClient MQTT(espClient);
  
//Prototypes
void init_serial(void);
//void init_display(void);						//TODO
void init_wifi(void);
void init_mqtt(void);
void reconnect_wifi(void); 
void mqtt_callback(char* topic, byte* payload, unsigned int length);
void verifica_conexoes_wifi_mqtt(void);
 
/* 
 *  Implementações das funções
 */
void setup() 
{
    init_serial();								// Inicia a porta serial
//	init_display();								//TODO
    init_wifi();								// Conecta à rede Wifi
    init_mqtt();								// Conecta ao broker MQTT


}
  
/* Função: inicializa comunicação serial com baudrate 115200 (para fins de monitorar no terminal serial 
*          o que está acontecendo.
* Parâmetros: nenhum
* Retorno: nenhum
*/
void init_serial() 
{
    Serial.begin(115200);
	Serial.println("Serial inicializada.");
}
 
 /* Função: inicializa o display.
* Parâmetros: nenhum
* Retorno: nenhum

void init_display() 
{
// SSD1306_SWITCHCAPVCC = generate display voltage from 3.3V internally
  if(!display.begin(SSD1306_SWITCHCAPVCC, 0x3C)) { 		//Ive changed the address //already chill
    Serial.println(F("SSD1306 allocation failed"));
    for(;;); 											// Don't proceed, loop forever
  }  
  display.clearDisplay();								// Clear the buffer 
  display.invertDisplay(true);							// Invert and restore display, pausing in-between
  delay(1000);
  display.invertDisplay(false);
  delay(1000);   
}
*/ 
 
 
/* Função: inicializa e conecta-se na rede WI-FI desejada
 * Parâmetros: nenhum
 * Retorno: nenhum
 */
void init_wifi(void) 
{
    delay(10);
    Serial.println("------Conexao WI-FI------");
    Serial.print("Conectando-se na rede: ");
    Serial.println(SSID);
    Serial.println("Aguarde");
    reconnect_wifi();
}
  
/* Função: inicializa parâmetros de conexão MQTT(endereço do  
 *         broker, porta e seta função de callback)
 * Parâmetros: nenhum
 * Retorno: nenhum
 */
void init_mqtt(void) 
{
    MQTT.setServer(BROKER_MQTT, BROKER_PORT); 	    // informa a qual broker e porta deve ser conectado
    MQTT.setCallback(mqtt_callback);            	// atribui função de callback
}
  
/* Função: função de callback 
 *          esta função é chamada toda vez que uma informação de 
 *          um dos tópicos subscritos chega
 * Parâmetros: nenhum
 * Retorno: nenhum
 * */
void mqtt_callback(char* topic, byte* payload, unsigned int length) 
{
    String msg;
    for(int i = 0; i < length; i++)    			 //obtem a string do payload recebido
    {
       char c = (char)payload[i];
       msg += c;
    }
    Serial.print("[MQTT] Mensagem recebida: ");
    Serial.println(msg);     
    Serial.print("Topico: ");
    Serial.println(topic);     
}
  
/* Função: reconecta-se ao broker MQTT (caso ainda não esteja conectado ou em caso de a conexão cair)
 *          em caso de sucesso na conexão ou reconexão, o subscribe dos tópicos é refeito.
 * Parâmetros: nenhum
 * Retorno: nenhum
 */
void reconnect_mqtt(void) 
{
    while (!MQTT.connected()) 
    {
        Serial.print("* Tentando se conectar ao Broker MQTT: ");
        Serial.println(BROKER_MQTT);
        if (MQTT.connect(ID_MQTT)) 
        {
            Serial.println("Conectado com sucesso ao broker MQTT!");
            MQTT.subscribe("lcabs1993");
            MQTT.subscribe("lcabs1993/arduino");
            MQTT.subscribe("lcabs1993/arduino/buzzer");
            MQTT.subscribe("lcabs1993/arduino/led");
            MQTT.subscribe("lcabs1993/arduino/dht11/temp");
            MQTT.subscribe("lcabs1993/arduino/dht11/humidade");
            MQTT.subscribe("lcabs1993/arduino/PIR01");      
        } 
        else
        {
            Serial.println("Falha ao reconectar ao broker.");
            Serial.println("Tentando reconectar em 2s...");
            delay(2000);
        }
    }
}
  
/* Função: reconecta-se ao WiFi
 * Parâmetros: nenhum
 * Retorno: nenhum
*/
void reconnect_wifi() 
{
    // se já está conectado a rede WI-FI, nada é feito. Caso contrário, são efetuadas tentativas de conexão
    if (WiFi.status() == WL_CONNECTED)
        return;
         
    WiFi.begin(SSID, PASSWORD);
     
    while (WiFi.status() != WL_CONNECTED) 
    {
        delay(100);
        Serial.print(".");
    }  
    Serial.println();
    Serial.print("Conectado com sucesso na rede "); 
    Serial.print(SSID);
    Serial.println("IP obtido: ");
    Serial.println(WiFi.localIP());
}
 
/* Função: verifica o estado das conexões WiFI e ao broker MQTT. 
 *         Em caso de desconexão (qualquer uma das duas), a conexão
 *         é refeita.
 * Parâmetros: nenhum
 * Retorno: nenhum
 */
void verifica_conexoes_wifi_mqtt(void)
{
    reconnect_wifi(); 								    // se não há conexão com o WiFI, a conexão é refeita 
    if (!MQTT.connected()) 								// se não há conexão com o Broker, a conexão é refeita
        reconnect_mqtt(); 
} 
 
/* programa principal */
void loop() 
{   
    /* garante funcionamento das conexões WiFi e ao broker MQTT */
    verifica_conexoes_wifi_mqtt();
    /* Envia frase ao broker MQTT */
    //MQTT.publish("lcabs1993/arduino", "ESP32 se comunicando com MQTT");
 
    /* keep-alive da comunicação com broker MQTT */    
    MQTT.loop();
    /* Agurda 1 segundo para próximo envio */
    delay(1000);   
}
